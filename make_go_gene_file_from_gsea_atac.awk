# make_go_gene_file_from_gsea_atac.awk

# example
# srun --mem=8G --pty bash
# cd ~/fragencode/workspace/sdjebali/geneswitch/wp5/atacseq/gsea.on.gn.with.peak.at.tss/lfcs/up.down.separate.abslfcs
# annot=~/fragencode/workspace/geneswitch/results/multi/forGSEA/Generic_human_ensemblIds_noParents.an.txt
# pgm=~/fragencode/tools/multi/Scripts/make_go_gene_file_from_gsea_atac.awk
# c1=liv_fet
# c2=HvsC
# direc=pos
# time head -11 $c1\_remove-32-outlier_motherboth_fatherboth_all_diet_$c2\_tsspeaks_lfc$direc\_gnid_onepeakpergn_forgsea_simple_sorted.out | awk -v direc=$direc -v fileRef1=$annot -v fileRef2=../../$c1\_remove-32-outlier_motherboth_fatherboth_all_diet_$c2\_tsspeaks_signedlfcall.pval_hsgnid_onepeakpergn_forgsea.tsv -f $pgm > ForInspection/$c1\_remove-32-outlier_motherboth_fatherboth_all_diet_$c2\_tsspeaks_lfc$direc\_hsgnid_onepeakpergn_forgsea_forinspection.tsv

# main input $c1\_remove-32-outlier_motherboth_fatherboth_all_diet_$c2\_tsspeaks_lfc$direc\_gnid_onepeakpergn_forgsea_simple_sorted.out
# term	termid	nbgn	corrmpfpval	corrpval	multifunc
# positive_regulation_of_lymphocyte_activation	GO:0051251	83	0.02117257	0.0270478	0.953
# 14 (6 fields)

# fileRef1=$annot
# # Annotation file generated by Gemma
# ...
# ProbeName	GeneSymbols	GeneNames	GOTerms	GemmaIDs	NCBIids
# ENSG00000271748					
# ENSG00000122548	KIAA0087	KIAA0087 lncRNA		202564	9808


# !!! need to look in this file if we have the right direction !!!
# fileRef2=../../$c1\_remove-32-outlier_motherboth_fatherboth_all_diet_$c2\_tsspeaks_signedlfcall.pval_hsgnid_onepeakpergn_forgsea.tsv 
# geneid	logFC	P.Value
# ENSG00000173320	0.074883	0.441996701427027
# 8086 (3 fields)

BEGIN{
    OFS="\t";
    # reads the first fileRef containing the GO (corres between genes and go terms)
    while (getline < fileRef1 >0)
    {
	split($0,a,"\t");
	# b will contain the list of go terms
	split(a[4],b,"|");
	# gnid will be gene name if it exists, otherwise gene id
	if(a[2]!="")
	{
	    gnid=a[2];
	}
	else
	{
	    gnid=a[1];
	}
	ensgn[gnid]=a[1];
	k=1;
	while(b[k]!="")
	{
	    nbgn[b[k]]++;
	    gene[b[k],nbgn[b[k]]]=gnid;
	    k++;
	}
    }

# geneid	logFC	P.Value
# ENSG00000173320	0.074883	0.441996701427027
# 8086 (3 fields)
    # reads the second fileRef containing whether the gene was used in the atac da analysis and what its lfc and pval were
    # and remember it only if it is from the right direction
    while (getline < fileRef2 >0)
    {
	if((direc=="pos"&&$2>=0)||(direc=="neg"&&$2<0))
	{
	    used[$1]=1;
	    lfc[$1]=$2;
	    pval[$1]=$3;
	}
    }
}

# reads the main input file with the results from gsea on the genes whose tss had an atacseq peak that went to da
# in the given comparison (tissue, stage, diet pair, direction)
# term	termid	nbgn	corrmpfpval	corrpval	multifunc
# positive_regulation_of_lymphocyte_activation	GO:0051251	83	0.02117257	0.0270478	0.953
# 14 (6 fields)
NR==1{
    print "gene", "ensid", "term", "termid", "nbgn", "corrmpfpval", "corrpval", "multifunc", "gene.used.in.da", "LFC", "Pval";
}

NR>=2{
    for(i=1; i<=nbgn[$2]; i++)
    {
	print gene[$2,i], ensgn[gene[$2,i]], $1, $2, $3, $4, $5, $6, (used[ensgn[gene[$2,i]]]==1 ? "yes" : "no"), (used[ensgn[gene[$2,i]]]==1 ? lfc[ensgn[gene[$2,i]]] : "NA"), (used[ensgn[gene[$2,i]]]==1 ? pval[ensgn[gene[$2,i]]] : "NA");
    }
} 
